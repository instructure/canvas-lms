#!/usr/bin/env groovy

/*
 * Copyright (C) 2020 - present Instructure, Inc.
 *
 * This file is part of Canvas.
 *
 * Canvas is free software: you can redistribute it and/or modify it under
 * the terms of the GNU Affero General Public License as published by the Free
 * Software Foundation, version 3 of the License.
 *
 * Canvas is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE. See the GNU Affero General Public License for more
 * details.
 *
 * You should have received a copy of the GNU Affero General Public License along
 * with this program. If not, see <http://www.gnu.org/licenses/>.
 */

library 'canvas-builds-library'
loadLocalLibrary('local-lib', 'build/new-jenkins/library')

// if the build never starts or gets into a node block, then we
// can never load a file. and a very noisy/confusing error is thrown.
def ignoreBuildNeverStartedError(block) {
  try {
    block()
  }
  catch (org.jenkinsci.plugins.workflow.steps.MissingContextVariableException ex) {
    if (!ex.message.startsWith('Required context class hudson.FilePath is missing')) {
      throw ex
    }
    else {
      echo "ignored MissingContextVariableException: \n${ex.message}"
    }
    // we can ignore this very noisy error
  }
}

def getMigrationsTag(name) {
  (env.GERRIT_REFSPEC.contains('master')) || !migrations.cacheLoadFailed() ? migrations.imageMergeTag(name) : migrations.imagePatchsetTag(name)
}

def getPatchsetTag() {
  (env.GERRIT_REFSPEC.contains('master')) ? "${configuration.buildRegistryPath()}:${env.GERRIT_BRANCH}" : imageTag.patchset()
}

def getResultsHTMLUrl() {
  return "${env.BUILD_URL}/artifact/results.html"
}

pipeline {
  agent { label 'canvas-docker' }
  options {
    ansiColor('xterm')
    timestamps()
  }

  environment {
    COMPOSE_PROJECT_NAME = 'main-axe'
    BUILD_REGISTRY_FQDN = configuration.buildRegistryFQDN()
    POSTGRES = configuration.postgres()
    RUBY = configuration.ruby() // RUBY_VERSION is a reserved keyword for ruby installs
    // e.g. canvas-lms:01.123456.78-postgres-12-ruby-2.6
    PATCHSET_TAG = getPatchsetTag()

    CASSANDRA_PREFIX = configuration.buildRegistryPath('cassandra-migrations')
    DYNAMODB_PREFIX = configuration.buildRegistryPath('dynamodb-migrations')
    POSTGRES_PREFIX = configuration.buildRegistryPath('postgres-migrations')

    IMAGE_CACHE_MERGE_SCOPE = configuration.gerritBranchSanitized()
    RSPEC_PROCESSES = 6

    CASSANDRA_IMAGE_TAG = "$CASSANDRA_PREFIX:$IMAGE_CACHE_MERGE_SCOPE-$RSPEC_PROCESSES"
    DYNAMODB_IMAGE_TAG = "$DYNAMODB_PREFIX:$IMAGE_CACHE_MERGE_SCOPE-$RSPEC_PROCESSES"
    POSTGRES_IMAGE_TAG = "$POSTGRES_PREFIX:$IMAGE_CACHE_MERGE_SCOPE-$RSPEC_PROCESSES"
  }

  stages {
    stage('Setup') {
      steps {
        cleanAndSetup()
      }
    }

    stage('Parallel Run Tests') {
      steps {
        script {
          def stages = [:]

          distribution.stashBuildScripts()
          rspecStage.createDistribution(stages)

          parallel(stages)
        }
      }
    }
  }

  post {
    always {
      script {
        ignoreBuildNeverStartedError {
          node('master') {
            buildSummaryReport.publishReport('Build Summary Report', currentBuild.getResult() == 'SUCCESS' ? 'SUCCESS' : 'FAILURE')
          }
        }

        copyArtifacts(
          filter: 'tmp/*_rspec_results/**',
          optional: false,
          projectName: env.JOB_NAME,
          selector: specific(env.BUILD_NUMBER),
        )

        withEnv(['COMPOSE_FILE=docker-compose.new-jenkins.yml']) {
          sh """
            docker-compose run -v \$(pwd)/\$LOCAL_WORKDIR/tmp/:/tmp \
            --name stormbreaker-results canvas bash -c \
            "bundle install; bundle exec rake stormbreaker:combine_results['/tmp/*_rspec_results/**']"
          """

          sh 'docker cp stormbreaker-results:/usr/src/app/results.html .'

          withCredentials([usernamePassword(credentialsId: 'INSENG_CANVAS_CI_AWS_ACCESS', usernameVariable: 'INSENG_AWS_ACCESS_KEY_ID', passwordVariable: 'INSENG_AWS_SECRET_ACCESS_KEY')]) {
            def awsCreds = "AWS_DEFAULT_REGION=us-west-2 AWS_ACCESS_KEY_ID=${INSENG_AWS_ACCESS_KEY_ID} AWS_SECRET_ACCESS_KEY=${INSENG_AWS_SECRET_ACCESS_KEY}"
            sh "$awsCreds aws s3 cp results.html s3://instructure-canvas-ci/axe_results.html"
          }

          archiveArtifacts allowEmptyArchive: true, artifacts: 'results.html'
        }

        // Only alert on periodic jobs, not ones resulting from manual tests
        if (env.GERRIT_EVENT_TYPE != 'comment-added') {
          slackSend channel: '#canvas-axe-build', message: "<$env.BUILD_URL/testReport|Latest Canvas Axe Selenium Results> - <${getResultsHTMLUrl()}|HTML>"
        }
      }
    }
    cleanup {
      script {
        ignoreBuildNeverStartedError {
          libraryScript.execute 'bash/docker-cleanup.sh --allow-failure'
        }
      }
    }
  }
}
